import 'utilities.eol';

model M driver EMF {nsuri="http://www.eclipse.org/emf/2002/Ecore"};

context ENamedElement {
	constraint NotReservedWord {
		check {
			return not self.name.isThriftReservedWord();
		}
		message : "Cannot use Thrift reserved word " + self.name + " as name for " + self.eClass().name
	}
}

context EAnnotation {
	// Number Annotation
	guard{
	return self.source = "thrift" and self.details.containsKey("n");
	}
	constraint IsEnumerable {
		check {
			return Set{"EAttribute", "EParameter"}.includes(self.getEModelElement().eClass().name);
		}
		message : self.getEModelElement().name + " must not have a thrift(n=...) annotation"
	}
}

context EAnnotation {
	// Java Namespace Annotation
//	guard : self.source = "thrift" and self.details.containsKey("javans")
	guard: 1 + 1
	constraint IsPackage {
		check {
			return self.getEModelElement().eClass().name = "EPackage";
		}
		message : self.getEModelElement().name + " is not a package and should not have a thrift(javans=...) annotation"
	}
	constraint IsValidPackageName {
		check {
			return self.details.get("javans").isJavaPackageName();
		}
		message : self.details.get("javans") + " is not a valid Java Package name"
	}
}

context EAnnotation {
	// Map annotation
	guard : self.source = "thrift.map"
	constraint IsOnStruct {
		check {
			return self.getEModelElement().eClass().name = "EClass" and self.getEModelElement().eOperations.isEmpty();
		}
		message : self.getEModelElement().name.ifUndefined("Unnamed object") + " is not a struct and should not have a @thrift.map annotaion."
	}
	constraint KeyValid {
		check {
			return self.details.containsKey("key") and self.getEModelElement().eContents.exists(field : EStructuralFeature | field.name = self.details.get("key"));
		}
		message : "thrift.map annotation has no or an invalid key field in " + self.getEModelElement().name 
	}
	constraint ValueValid{
		check {
			return self.details.containsKey("value") and self.getEModelElement().eContents.exists(field : EStructuralFeature | field.name = self.details.get("value"));
		}
		message : "thrift.map annotation has no or an invalid value field in " + self.getEModelElement().name
	} 

}

context ETypedElement {
	constraint IsValidThriftType {
		check {
			return self.getType() <> "unknown";
		}
		message : self.eType + " is not a valid type!"
	}

}

context EAnnotation {
	// @doc(auth...) annotations
	guard : self.source = "doc" and self.details.containsKey("auth")
	constraint IsOperation {
		check {
			return self.getEModelElement().eClass().name = "EOperation";
		}
		message : self.getEModelElement().name + " is not an operation and should not have a doc(auth=...) attribute!"
	}
	
	constraint IsBoolean {
		check {
			return Set{"false","true"}.includes(self.details.get("auth"));
		}
		message : self.details.get("auth") + " is not a boolean value! In a @doc(auth=...) annotation in " + self.getEModelElement().name
	}
}

context EAnnotation {
	// Optional annotations
	guard : self.source = "doc" and self.details.containsKey("optional")
	constraint IsArgument {
		check {
			return Set{"EParameter", "EAttribute", "EReference"}.includes(self.getEModelElement().eClass.name);
		}
		message : self.getEModelElement().eContainer.name + "::" + self.getEModelElement().name + " is not an argument nor a field, but a " + self.getEModelElement().eClass.name + " and should not have a doc(optional=...) attribute"
	}
	constraint IsBoolean {
		check {
			return Set{"false","true"}.includes(self.details.get("optional"));
		}
		message : self.details.get("optional") + " is not a boolean value! In @doc(optional=...) annotation in " + self.getEModelElement().name
	}
}

context EClass {
	constraint OperationsOrFields {
		check {
			return self.eOperations.isEmpty() or self.eAttributes.isEmpty();
		}
		message : "You must have either no operations or no fields in " + self.name
	}
}

context EClass {
	// No operations, ie., is a struct or exception
	guard : self.eOperations.isEmpty()

	constraint ValidEnumeration {
		check {
			// the number annotation is valid when it exists
			// TODO: check that they are unique! (including the ones without explicit numbers)
			return self.eStructuralFeatures.forAll(annotation | annotation.hasNumberAnnotation() implies annotation.isNumberAnnotationValid());
		}
		message : "@thrift(n=...) annotations must have an integer value!"
	}
	
	constraint FieldNumbering {
		check {
			return self.eAttributes.numberAnnotationsUnique();
		}
	}
	
	constraint EitherExceptionOrStruct {
		check {
			return not (self.isUsedAsException() and self.isUsedAsStruct());
		}
		message : self.name + " is used as both an exception and a struct!"
	}
	
	critique UsedAsExceptionOrStruct {
		check {
			return self.isUsedAsException() or self.isUsedAsStruct();
		}
		message : self.name + " is not used!"
	} 
}

context EClass {
	// is a service
	guard : self.eAttributes.isEmpty() and not self.eOperations.isEmpty()
	
	constraint NoNumber {
		check {
			return not self.hasNumberAnnotation();
		}
		message : self.name + " must not have a @thrift(n=...) annotation!"
	}
}

context EOperation {
	constraint ParameterNumbering {
		check {
			return self.eParameters.numberAnnotationsUnique();
		}
		message : "All parameters should have unique numbers in " + self.name
	}
	
	constraint ExceptionNumbering {
		check {
			return self.eExceptions.numberAnnotationsUnique();
		}
		message : "All exceptions should have unique numbers in " + self.name
	}
}

// Test markers
//! [30:1-30:13] Expression type should be Boolean instead of Integer